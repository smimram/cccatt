# Cartesian bicategories
#
# See for instance _A type theory for cartesian closed bicategories_ by Fiore and Saville
#
#-# mode: cartesian
#-# dimension: 2

include "bicategory.cccatt"

# tup
coh pa {a b c : .} (f : a -> b) (g : a -> c) : a -> b * c

# π
coh fst {a b : .} : a * b -> a

coh snd {a b : .} : a * b -> b

# ϖ
coh pa-fst {a b c : .} (f : a -> b) (g : a -> c) : co1 (pa f g) fst -> f

coh pa-snd {a b c : .} (f : a -> b) (g : a -> c) : co1 (pa f g) snd -> g

# p†
coh pa2 {a b c : .} {f : a -> b} {g : a -> c} (h : a -> b * c) (α : co1 h fst -> f) (β : co1 h snd -> g) : h -> pa f g

## Note that eta-expansion is derivable
# coh pa-eta {a b c : .} (f : a -> b * c) : f -> pa (co1 f fst) (co1 f snd)
# ς
let pa-eta {a b c : .} (f : a -> b * c) : f -> pa (co1 f fst) (co1 f snd) := pa2 f (id2 (co1 f fst)) (id2 (co1 f snd))

# U1
coh pa2-fst {a b c : .} {f : a -> b} {g : a -> c} (h : a -> b * c) (α : co1 h fst -> f) (β : co1 h snd -> g) : co2 (co21 (pa2 h α β) fst) (pa-fst f g) = α

coh pa2-snd {a b c : .} {f : a -> b} {g : a -> c} (h : a -> b * c) (α : co1 h fst -> f) (β : co1 h snd -> g) : co2 (co21 (pa2 h α β) snd) (pa-snd f g) = β

# U2
coh pa2-eta {a b c : .} {f : a -> b} {g : a -> c} {h : a -> b * c} (α : h -> pa f g) : α = pa2 h (co2 (co21 α fst) (pa-fst f g)) (co2 (co21 α snd) (pa-snd f g))

# cong
coh pa2-cong {a b c : .} {f : a -> b} {g : a -> c} {h : a -> b * c} {α α' : co1 h fst -> f} {β β' : co1 h snd -> g} (_ : α -> α') (_ : β -> β') : pa2 h α β = pa2 h α' β'

## Admissible rules

coh pa2 {a b c : .} {f f' : a -> b} {g g' : a -> c} (α : f -> f') (β : g -> g') : pa f g -> pa f' g'

coh pa2-id2 {a b c : .} (f : a -> b) (g : a -> c) : pa2 (id2 f) (id2 g) -> id2 (pa f g)

coh pa2-co2 {a b c : .} {f f' f'' : a -> b} {g g' g'' : a -> c} (α : f -> f') (α' : f' -> f'') (β : g -> g') (β' : g' -> g'') : co2 (pa2 α β) (pa2 α' β') = pa2 (co2 α α') (co2 β β')

# ς-nat
coh pa-eta-nat {a b c : .} {f f' : a -> b * c} (α : f -> f') : co2 α (pa-eta f') = co2 (pa-eta f) (pa2 (co21 α fst) (co21 α snd))

# ϖ-nat
coh pa-fst-nat {a b c : .} {f f' : a -> b} {g g' : a -> c} (α : f -> f') (β : g -> g') : co2 (co21 (pa2 α β) fst) (pa-fst f' g') = co2 (pa-fst f g) α

coh pa-snd-nat {a b c : .} {f f' : a -> b} {g g' : a -> c} (α : f -> f') (β : g -> g') : co2 (co21 (pa2 α β) snd) (pa-snd f' g') = co2 (pa-snd f g) β

# triangle-law-1
coh tl1 {a b c : .} (f : a -> b) (g : a -> c) : co2 (pa-eta (pa f g)) (pa2 (pa-fst f g) (pa-snd f g)) = id2 _

# triangle-law-2
coh tl2-fst {a b c : .} (f : a -> b * c) : co2 (co21 (pa-eta f) fst) (pa-fst _ _) = id2 (co1 f fst)

coh tl2-snd {a b c : .} (f : a -> b * c) : co2 (co21 (pa-eta f) snd) (pa-snd _ _) = id2 (co1 f snd)
