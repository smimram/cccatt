#-# mode: monoidal

# Categories

coh id {a : .} : a -> a

coh co {a b c : .} (f : a -> b) (g : b -> c) : a -> c

let co3 {a b c d : .} (f : a -> b) (g : b -> c) (h : c -> d) := co f (co g h)

coh unitl {a b : .} (f : a -> b) : co id f = f

coh unitr {a b : .} (f : a -> b) : co f id = f

coh assoc {a b c d : .} (f : a -> b) (g : b -> c) (h : c -> d) : co (co f g) h = co f (co g h)

# Monoidal categories

coh te {a a' b b' : .} (f : a -> a') (g : b -> b') : a * b -> a' * b'

coh assoc {a b c : .} : (a * b) * c -> a * (b * c)

coh assoc' {a b c : .} : a * (b * c) -> (a * b) * c

coh unitl {a : .} : 1 * a -> a

coh unitl' {a : .} : a -> 1 * a

coh unitr {a : .} : a * 1 -> a

coh unitr' {a : .} : a -> a * 1

## Invertibility

coh unitl-unitl' {a : .} : co unitl unitl' = id {1 * a}

coh unitl'-unitl {a : .} : co unitl' unitl = id {a}

coh unitr-unitr' {a : .} : co unitr unitr' = id {a * 1}

coh unitr'-unitr {a : .} : co unitr' unitr = id {a}

coh assoc-assoc' {a b c : .} : co assoc assoc' = id {(a * b) * c}

coh assoc'-assoc {a b c : .} : co assoc' assoc = id {a * (b * c)}

## Naturality

coh unitl-nat {a b : .} (f : a -> b) : co (te id f) unitl = co unitl f

coh unitr-nat {a b : .} (f : a -> b) : co (te f id) unitr = co unitr f

coh assoc-nat {a a' b b' c c' : .} (f : a -> a') (g : b -> b') (h : c -> c') : co (te (te f g) h) assoc = co assoc (te f (te g h))

## Axioms

coh triangle {a b : .} : co assoc (te id unitl) ={ (a * 1) * b -> a * b } te unitr id

coh pentagon {a b c d : .} : co3 (te assoc id) assoc (te id assoc) ={ ((a * b) * c) * d -> a * (b * (c * d)) } co assoc assoc
